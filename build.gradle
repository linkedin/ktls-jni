plugins {
    id 'idea'
    id 'java-library'
    id 'maven-publish'
}

group 'com.linkedin.ktls'

repositories {
//    Uncomment mavenLocal to publish artifacts to local maven
//    mavenLocal()
    mavenCentral()
}

java {
    toolchain {
//        Used java version 11 for enabling supported cipher suites for enabling KTLS
        languageVersion = JavaLanguageVersion.of(11)
    }
}

dependencies {
    testImplementation 'org.junit.jupiter:junit-jupiter-api:5.8.1'
    testImplementation 'org.junit.jupiter:junit-jupiter-params:5.8.1'
    testImplementation 'org.bouncycastle:bcpkix-jdk18on:1.71.1'
    testImplementation 'commons-codec:commons-codec:1.15'
    testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.7.0'
}

test {
    useJUnitPlatform()
}

sourceSets {
    main {
        resources {
            srcDirs "src/main/resources", "build/natives/lib"
        }
    }
}

compileJava {
    sourceCompatibility = '1.8'
    targetCompatibility = '1.8'
    options.compilerArgs += ["-h", file("src/main/include/jni_generated")]
}

def os = System.getProperty("os.name").toLowerCase()
def isLinux = os.contains("linux")

task compileJNI {
    dependsOn compileJava
    doLast {
        // Run the build-native.sh script if the "buildType" flag is set
        if (project.hasProperty('buildType') && project.getProperty('buildType') == 'native') {
            exec {
                commandLine 'scripts/build-native.sh'
            }
        } else {
            exec {
                commandLine 'scripts/prepare-docker-image.sh'
            }
            exec {
                commandLine 'scripts/build-linux-on-docker.sh'
            }
        }
    }
}

clean.doFirst {
    delete fileTree('src/main/include/jni_generated') {
        include '*.h'
    }
}

processResources {
    dependsOn compileJNI
}

tasks.withType(Test) {
    systemProperty "java.library.path", "build/natives/lib"
    testLogging.showStandardStreams = true
}

publishing {
    publications {
        maven(MavenPublication) {
            version = project.getProperty('version')
            from components.java
        }
    }
}

publishToMavenLocal.dependsOn(assemble)

task install {
    dependsOn publishToMavenLocal
}
